task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

allprojects {
    version = '4.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }
}

subprojects {
    // all projects must be compatible with the java version specified here
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    tasks.withType(Test) {
        scanForTestClasses = false
        include "**/*Test.class"
    }

}

project(':') {
    dependencies {
        compile project(':gui')
    }
}

project(':core') {
	test {
		testLogging.showStandardStreams = true
		testLogging.showExceptions = true
		 
		afterTest { descriptor, result ->
		    if (result.getResultType() == TestResult.ResultType.FAILURE) {
     			logger.lifecycle("FAILED test: " + descriptor)
     		}
  		}
	}
}

project(':gui') {
    dependencies {
        compile project(":extra")
        compile group: 'de.jensd', name: 'fontawesomefx', version: '8.2'
        compile group: 'org.controlsfx', name: 'controlsfx', version: '8.20.8'
    }
}

project(':extra') {
    dependencies {
        compile project(":core")
    }
}

task AIMADemoApp(type: JavaExec, dependsOn: classes) {
    main = 'aima.gui.app.AIMADemoApp'
    classpath = files(sourceSets.main.output, configurations.runtime)
}
